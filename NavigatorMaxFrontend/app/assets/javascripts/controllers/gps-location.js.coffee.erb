window.GpsLocation = class
  constructor:  ->
    @gps_table = new DataTableView '#phidget_gps_coords'

    default_latlng = new google.maps.LatLng(40.6892, -74.0447)
    @google_map = new google.maps.Map( $('#gps_map')[0], 
      { zoom: 21, mapTypeId: google.maps.MapTypeId.SATELLITE, center: default_latlng, zoomControl: true, tilt: 0 } )
    @marker = new google.maps.Marker({ position: default_latlng, map: @google_map, zIndex: 1 })

    @orientation = new VectorPlot2D $("#heading_gps_vector")[0], 
      "Heading Vector", 'N', 'E', VECTOR_COLORS

  set_metadata: (data) -> 
    attribs = data.gps_attributes
    $('#gps-location .title_annotation').html("#{attribs.name} (serial #{attribs.serial_number}) v.#{attribs.version}")
    
  tick: (data) ->
    @gps_table.set(property, value) for property, value of { 
      device_comm_rate: "#{data.gps.updates_per_second} hz", 
      fixed: (if data.gps.is_fixed then 'Yes' else 'No'),
      latitude: data.gps.latitude, longitude: data.gps.longitude, 
      altitude: data.gps.altitude, heading: data.gps.heading, 
      velocity: data.gps.velocity, utc_time: data.gps.time}
    @gps_table.render()

    position = new google.maps.LatLng(data.gps.latitude, data.gps.longitude)
    @marker.setPosition(position)
    @google_map.panTo(position)

  render: ->
    @orientation.render()
