window.SystemController = class
  constructor: () ->
    @last_snapshot_at = null

    # Load the Colors
    @cpu_system_color =  '#'+decimal_to_hex_string(COLORS.cpu_system)
    @cpu_user_color =  '#'+decimal_to_hex_string(COLORS.cpu_user)
    @process_system_color =  '#'+decimal_to_hex_string(COLORS.process_system)
    @process_user_color =  '#'+decimal_to_hex_string(COLORS.process_user)

    @tables = { 
      system: 'system_info',
      process: 'navigatormax_process',
      kernel: 'kernel_stats',
      network: 'network_stats',
      memory: 'memory_management'}

    @tables[label] = new DataTableView("##{table}") for label, table of @tables

    #### CPU Chart setup
    @totalPoints = 300
    @cpu_system_percent = []
    @cpu_user_percent = []
    @process_system_percent = []
    @process_user_percent = []
    for i in [0..@totalPoints-1] by 1
      @cpu_system_percent.push 1
      @cpu_user_percent.push 1
      @process_system_percent.push 1
      @process_user_percent.push 1

    options = {
      grid: {
        borderWidth: 1, minBorderMargin: 20, labelMargin: 10,
        backgroundColor: { colors: ["#fff", "#fafafa"] },
        margin: { top: 8, bottom: 18, left: 45 },
        markings: (axes) ->
          markings = []
          xaxis = axes.xaxis
          for x in [Math.floor(xaxis.min)..xaxis.max] by xaxis.tickSize * 2
            markings.push({ xaxis: { from: x, to: x + xaxis.tickSize }, color: "rgba(232, 232, 255, 0.2)" })
          markings
      },
      series: {stack: true, lines: { show: true, fill: true} , shadowSize: 0}, 
      xaxis: { tickFormatter: (i) => "-#{@totalPoints - i} sec" },
      yaxis: { min: 0, max: 100, tickFormatter: (i) -> "<span style='margin-left: -15px'>#{i}</span>" },
      legend: { show: true, position: "nw" }
    }
    container = $('#cpu_load_graph')
    @plot = $.plot(container,  @_cpu_plot_format() , options)

    yaxisLabel = $("<div class='axisLabel yaxisLabel'>CPU Usage (%)</div>")
    yaxisLabel.appendTo(container)
    width = $('#cpu_load_graph .yaxisLabel').width()
    width =  107 # TODO! this needs to calculate on the first render
    yaxisLabel.css("margin-top", width / 2 - 20)
    

  set_metadata: (data) -> 
    attribs = data.system
    $('#system .title_annotation').html("#{attribs.hostname} (serial #{attribs.serial_number})")

    @tables.system.set(property, value) for property, value of { 
      uname: attribs.uname, boot_time: attribs.boot_time,
      cpu_arch: attribs.cpu_arch }

    @tables.process.set(property, value) for property, value of { 
      interpreter: attribs.ruby_description }
    @tables.network.set(property, value) for property, value of { 
      primary_interface: attribs.primary_interface }
    @tables.memory.set(property, value) for property, value of { 
      total: "#{format_number(attribs.memory_total / 1024 / 1024, 2)} MiB" }
    @tables.kernel.set(property, value) for property, value of { 
      root_fs: attribs.root_filesystem  }
    
  tick: (data) ->
    system = data.system
    if system.snapshot_at != @last_snapshot_at
      @last_snapshot_at = system.snapshot_at

      load_avg = system.load_avg.map( (avg,i) -> avg.toFixed(2) ).join(', ')
      
      cpu_usage = { 
        user: system.cpu_percent_user, 
        system: system.cpu_percent_system, 
        idle: system.cpu_percent_idle,
        process_total: system.process_percent_system+system.process_percent_user,
        process_user: system.process_percent_user,
        process_system: system.process_percent_system}
      for label, value of cpu_usage 
        cpu_usage[label] = "#{value.toFixed(2)}%"

      # There's better ways to do this, but this works for now:
      uptime = system.uptime
      up_days = Math.floor(uptime / 3600 / 24)
      uptime -= up_days * 3600 * 24
      up_hours = Math.floor( uptime / 3600 )
      uptime -= up_hours * 3600
      up_min = Math.floor( uptime / 60 )
      up_sec = uptime - up_min * 60

      uptime = "#{up_days} days #{up_hours} hours #{up_min} min #{up_sec} sec" 

      @tables.process.set(property, value) for property, value of { 
        process_cpu: cpu_usage.process_total,
        process_user: cpu_usage.process_user, process_system: cpu_usage.process_system,
        resident_memory: "#{(system.process_resident_memory/1024/1024).toFixed(2)} MiB",
        comm_rate: "#{system.updates_per_second} hz", 
        gc_rate: "#{system.gc_rate.toFixed(2)} hz"}
      @tables.kernel.set(property, value) for property, value of { 
        load_avg: load_avg, cpu_idle: cpu_usage.idle, 
        cpu_user: cpu_usage.user, cpu_system: cpu_usage.system,
        uptime: uptime , root_fs_free: system.root_filesystem_free  }
      @tables.network.set(property, value) for property, value of { 
        wifi_network: system.wifi_network, signal: system.wifi_signal, 
        noise: system.wifi_noise,
        send_rate: "#{(system.network_send_rate/1024).toFixed(2)} KiB",
        receive_rate: "#{(system.network_recv_rate/1024).toFixed(2)} KiB" }
      @tables.memory.set(property, value) for property, value of { 
        free: "#{format_number(system.memory_free / 1024 / 1024, 2)} MiB",
        swap_rate: system.memory_swaprates}

      # Update the CPU Graph:

      @cpu_system_percent = @cpu_system_percent.slice(1) 
      @cpu_system_percent.push parseInt(system.cpu_percent_system-system.process_percent_system)
      @cpu_user_percent = @cpu_user_percent.slice(1) 
      @cpu_user_percent.push parseInt(system.cpu_percent_user-system.process_percent_user)
      @process_system_percent = @process_system_percent.slice(1)
      @process_system_percent.push parseInt(system.process_percent_system)
      @process_user_percent = @process_user_percent.slice(1)
      @process_user_percent.push parseInt(system.process_percent_user)

      @plot.setData(@_cpu_plot_format()) if @plot

  render: ->
    table.render() for label, table of @tables
    @plot.draw() if @plot

  _to_plot_format: (data) ->
    ret = []
    ret.push([i, value]) for value,i in data
    ret

  _cpu_plot_format: ->
    [ { label: 'CPU System', color: @cpu_system_color , data: @_to_plot_format(@cpu_system_percent) }, 
      { label: 'Process System', color: @process_system_color, data: @_to_plot_format(@process_system_percent)},
      { label: 'CPU User', color: @cpu_user_color, data: @_to_plot_format(@cpu_user_percent)},
      { label: 'Process User', color: @process_user_color, data: @_to_plot_format(@process_user_percent)}
    ]

